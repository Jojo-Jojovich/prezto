#
# Exposes information about the Node.js environment via the $node_info
# associative array.
#
# Authors:
#   Zeh Rizzatti <zehrizzatti@gmail.com>
#

local version
local version_format
local version_formatted

unset node_info
typeset -gA node_info

if (( $+functions[nvm_version] )); then
  version="node-${$(nvm_version)#v}"

elif (( $+functions[nodenv] )); then
  if [ -d "/usr/local/Cellar/nodenv-package-json-engine" ] && [ ! -z "$(nodenv package-json-engine)" ] && [[ "$(nodenv package-json-engine)" =~ "[0-9]+.[0-9]+.[0-9]" ]]; then
    version="node-${$(nodenv package-json-engine)#v}"
  elif [[ ! -z "$(nodenv version-name)" ]]; then
    version="node-${$(nodenv version-name)#v}"
  fi
fi

if [[ "$version" != (none|node-|) ]]; then
  zstyle -s ':prezto:module:node:info:version' format 'version_format'
  zformat -f version_formatted "$version_format" "v:$version"
  node_info[version]="$version_formatted"
fi
